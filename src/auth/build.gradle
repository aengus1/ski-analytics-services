apply plugin: 'java'
apply plugin: 'ski.crunch.build.deploy'
//logging.captureStandardOutput LogLevel.INFO
dependencies {
    
    compileOnly(project(':common'))  //exclude from packaged artifact -> provided at runtime by lambda container
    implementation(
            "com.amazonaws:aws-lambda-java-events:2.2.6",
            "com.auth0:java-jwt:3.8.0",
            "com.auth0:jwks-rsa:0.8.2",
            //mem-hard pw hashing implementation https://github.com/terl/lazysodium-java
            "com.goterl.lazycode:lazysodium-java:4.2.5",
            "co.libly:resource-loader:1.3.7",
            "net.java.dev.jna:jna:5.5.0"
    )

    integrationTestImplementation(
            "org.junit.jupiter:junit-jupiter-api:5.4.0",
            "org.glassfish.jersey.core:jersey-client:2.5.1",
            "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}",
            "org.mockito:mockito-all:1.10.19",
            project(path: ':common', configuration: 'testClasses'),
            project(':common')
    )
    integrationTestRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:5.1.0",
            "javax.xml.bind:jaxb-api:2.3.1",
            project(':common'),
            project(path: ':common', configuration: 'testClasses')
    )

}

test {
    include 'ski/crunch/**'
}

/**
 * Not sure why but clean task is not properly cleaning the build dir.. this is a temp fix
 */
//task customCleanUp(type:Delete) {
//    doFirst {
//        delete(buildDir)
//    }
//}
//tasks.clean.dependsOn(tasks.customCleanUp)



deploy.dependencies = ['cf-userpool-trg', 'common' ]


/**
 * Task for building the zip file for upload
 *  This is the desired configuration for 'java' plugin modules
 *  Includes only the dependencies declared in 'implementation' configuration (and their deps)  but excludes
 *  libraries declared in common, as they will already be available on classpath via lambda layers
 *  See gradle documentation here: https://docs.gradle.org/current/userguide/java_plugin.html
 *  Also the gradle 'java-library' plugin documentation contains further details on the api vs implementation
 *  https://docs.gradle.org/current/userguide/java_library_plugin.html
 */
task buildZip(type: Zip) {
    archiveBaseName = "${project.name}"
    from compileJava
    from processResources
    into('lib') {
        from ([ configurations.runtimeClasspath ] - [ configurations.compileOnly ])
    }

}

build.dependsOn buildZip

